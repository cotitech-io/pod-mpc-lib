// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.19;

import "@openzeppelin/contracts/access/Ownable.sol";

/// @title MinerBase
/// @notice Ownable registry of addresses allowed to call miner-only inbox functions.
contract MinerBase is Ownable {
    mapping(address => bool) private _miners;

    event MinerAdded(address miner);
    event MinerRemoved(address miner);

    /// @dev Reverts unless `msg.sender` is a registered miner.
    modifier onlyMiner() {
        require(_miners[msg.sender], "MinerBase: caller is not a miner");
        _;
    }

    /// @param initialOwner Initial {Ownable} owner.
    constructor(address initialOwner) Ownable(initialOwner) {}

    /// @notice Register a miner address.
    /// @param miner Address allowed to mine.
    function addMiner(address miner) external onlyOwner {
        require(miner != address(0), "MinerBase: miner is zero address");
        require(!_miners[miner], "MinerBase: already a miner");
        _miners[miner] = true;
        emit MinerAdded(miner);
    }

    /// @notice Remove a miner address.
    /// @param miner Address to revoke.
    function removeMiner(address miner) external onlyOwner {
        require(_miners[miner], "MinerBase: not a miner");
        delete _miners[miner];
        emit MinerRemoved(miner);
    }

    /// @notice Whether `miner` is registered.
    /// @param miner Address to query.
    /// @return True if registered.
    function isMiner(address miner) external view returns (bool) {
        return _miners[miner];
    }
}
